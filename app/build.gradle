apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

android {
    compileSdkVersion 29

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kapt {
        javacOptions {
            option("-source", "8")
            option("-target", "8")
        }
    }

    defaultConfig {
        applicationId "online.toolboox.v1"
        minSdkVersion 26
        targetSdkVersion 29

        versionCode parentVersionCode
        versionName parentVersionName

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    output.outputFileName = "${applicationName}-${variant.flavorName}-${variant.buildType.name}-${defaultConfig.versionName}.apk"
            }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    ext {
        appName = 'toolboox'
    }

    flavorDimensions 'tier'
    productFlavors {
        prod {
            resValue "string", "app_name", "ToolBoox"
            applicationIdSuffix ""
            dimension 'tier'
        }
    }

    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            storePassword project.property('toolBooxStorePassword').toString()
            keyAlias "key0"
            keyPassword project.property('toolBooxKeyPassword').toString()
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            debuggable true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            debuggable false

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }
    }
    packagingOptions {
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    implementation 'com.jakewharton:butterknife:7.0.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation "com.github.tony19:named-regexp:0.2.5"

    // AndroidX dependencies
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.fragment:fragment:1.2.5'

    // Material design
    implementation 'com.google.android.material:material:1.3.0-beta01'
    implementation 'com.android.support:support-annotations:28.0.0'

    // Dagger DI and its dependency
    kapt 'com.google.dagger:dagger-compiler:2.27'
    implementation 'com.google.dagger:dagger:2.27'
    compileOnly "org.glassfish:javax.annotation:3.1.1"

    // Onyx SDK
    implementation('com.onyx.android.sdk:onyxsdk-base:1.5.8')
    implementation('com.onyx.android.sdk:onyxsdk-data:1.1.7')
    implementation('com.onyx.android.sdk:onyxsdk-device:1.1.4')
    implementation('com.onyx.android.sdk:onyxsdk-hwr:1.0.1')
    implementation('com.onyx.android.sdk:onyxsdk-notedata:1.1.1@aar')
    implementation('com.onyx.android.sdk:onyxsdk-pen:1.1.3')

    // Onyx SDK dependency
    kapt "com.github.Raizlabs.DBFlow:dbflow-processor:4.2.4"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:4.2.4"
    implementation "com.github.Raizlabs.DBFlow:dbflow:4.2.4"

    // OkHttp, Retrofit and GSON
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.5.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.google.code.gson:gson:2.8.5'

    // Kotlin and extensions
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.4.21'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1'

    // Firebase integrations
    implementation platform('com.google.firebase:firebase-bom:26.1.1')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    // Test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

apply plugin: 'com.google.gms.google-services'
