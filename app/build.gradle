apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.dagger.hilt.android'

android {
    compileSdkVersion 32

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kapt {
        javacOptions {
            option("-source", "8")
            option("-target", "8")
        }
    }
    kapt.includeCompileClasspath = false

    defaultConfig {
        applicationId "com.toolsboox"
        minSdkVersion 26
        targetSdkVersion 32

        versionCode parentVersionCode
        versionName parentVersionName

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    applicationVariants.all {
        variant ->
            variant.outputs.each {
                output ->
                    output.outputFileName = "${applicationName}-${variant.flavorName}-${variant.buildType.name}-${defaultConfig.versionName}.apk"
            }
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    ext {
        appName = 'toolboox'
    }

    flavorDimensions 'tier'
    productFlavors {
        prod {
            resValue "string", "app_name", "Tools for Boox"
            applicationIdSuffix ""
            dimension 'tier'
        }
    }

    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            if (project.hasProperty('toolBooxStorePassword')) {
                storePassword project.property('toolBooxStorePassword').toString()
            } else {
                storePassword System.getenv("KEYSTORE_PASSWORD")
            }
            keyAlias "key0"
            if (project.hasProperty('toolBooxKeyPassword')) {
                keyPassword project.property('toolBooxKeyPassword').toString()
            } else {
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            debuggable true

            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Tools for Boox (debug)"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            debuggable false

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'MissingQuantity'
    }

    packagingOptions {
        jniLibs {
            pickFirsts += [
                    'lib/arm64-v8a/libc++_shared.so',
                    'lib/armeabi-v7a/libc++_shared.so',
                    'lib/x86/libc++_shared.so',
                    'lib/x86_64/libc++_shared.so']
        }
    }

    namespace 'com.toolsboox'
}

dependencies {
    // Commons
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Billing client
    implementation 'com.android.billingclient:billing:5.1.0'
    implementation 'com.android.billingclient:billing-ktx:5.1.0'

    // AndroidX dependencies
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.fragment:fragment-ktx:1.5.4'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-savedstate:2.5.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'

    // Material design
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'com.android.support:support-annotations:28.0.0'

    // Dagger and Hilt DI and its dependency
    kapt 'com.google.dagger:dagger-compiler:2.44.2'
    implementation 'com.google.dagger:dagger:2.44.2'
    kapt 'com.google.dagger:hilt-android-compiler:2.44.2'
    implementation 'com.google.dagger:hilt-android:2.44.2'

    // Onyx SDK
    implementation 'com.onyx.android.sdk:onyxsdk-base:1.6.39'
    implementation 'com.onyx.android.sdk:onyxsdk-device:1.2.15'
    implementation 'com.onyx.android.sdk:onyxsdk-pen:1.4.2'

    // OkHttp, Retrofit and GSON
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.14.0'
    implementation 'com.squareup.moshi:moshi:1.14.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.10.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.google.code.gson:gson:2.10'

    // Kotlin and extensions
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // Firebase integrations
    implementation platform('com.google.firebase:firebase-bom:31.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}

apply plugin: 'com.google.gms.google-services'
