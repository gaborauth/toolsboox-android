plugins {
    id 'com.android.application'

    id 'com.google.devtools.ksp'
    id 'com.google.dagger.hilt.android'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'

    id 'kotlin-android'
}

android {
    compileSdk 35

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId "com.toolsboox"
        minSdkVersion 26
        targetSdkVersion 35

        versionCode parentVersionCode
        versionName parentVersionName

        multiDexEnabled = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    applicationVariants.configureEach {
        variant ->
            variant.outputs.each {
                output ->
                    output.outputFileName = "${applicationName}-${variant.flavorName}-${variant.buildType.name}-${defaultConfig.versionName}.apk"
            }
    }

    packagingOptions {
        exclude("META-INF/DEPENDENCIES")
        exclude("META-INF/INDEX.LIST")
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    ext {
        appName = 'toolboox'
    }

    flavorDimensions = ['tier']
    productFlavors {
        dev {
            resValue "string", "app_name", "Tools for Boox - Dev"
            applicationIdSuffix ""
            dimension 'tier'
        }
        prod {
            resValue "string", "app_name", "Tools for Boox"
            applicationIdSuffix ""
            dimension 'tier'
        }
    }

    signingConfigs {
        release {
            storeFile file("../keystore.jks")
            if (project.hasProperty('toolBooxStorePassword')) {
                storePassword project.property('toolBooxStorePassword').toString()
            } else {
                storePassword System.getenv("KEYSTORE_PASSWORD")
            }
            keyAlias "key0"
            if (project.hasProperty('toolBooxKeyPassword')) {
                keyPassword project.property('toolBooxKeyPassword').toString()
            } else {
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true

            applicationIdSuffix ".debug"
            resValue "string", "app_name", "Tools for Boox (debug)"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue("string", "ad_unit_id_dashboard", "ca-app-pub-3940256099942544/6300978111")
        }
        release {
            minifyEnabled false
            shrinkResources false
            debuggable false

            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFiles fileTree('proguard').asList().toArray()

            resValue("string", "ad_unit_id_dashboard", "ca-app-pub-2136545277716623/7789111670")
        }
    }


    packagingOptions {
        jniLibs {
            pickFirsts += [
                    'lib/arm64-v8a/libc++_shared.so',
                    'lib/armeabi-v7a/libc++_shared.so',
                    'lib/x86/libc++_shared.so',
                    'lib/x86_64/libc++_shared.so']
        }
    }

    namespace 'com.toolsboox'
    lintOptions {
        disable 'MissingTranslation'
        disable 'MissingQuantity'
    }
}

dependencies {
    // Commons
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // Google clients and libraries
    implementation 'com.android.billingclient:billing:7.1.1'
    implementation 'com.android.billingclient:billing-ktx:7.1.1'
    implementation 'com.google.android.gms:play-services-ads:23.6.0'
    implementation 'com.google.android.gms:play-services-auth:21.3.0'
    implementation 'com.google.android.play:review:2.0.2'
    implementation 'com.google.android.play:review-ktx:2.0.2'
    implementation 'com.google.api-client:google-api-client-android:2.7.2'
    implementation 'com.google.apis:google-api-services-drive:v3-rev20250122-2.0.0'
    implementation 'com.google.mlkit:digital-ink-recognition:18.1.0'

    // AndroidX dependencies
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.8.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.8.7'

    // Dagger and Hilt DI and its dependency
    ksp 'com.google.dagger:hilt-android-compiler:2.55'
    implementation 'com.google.dagger:hilt-android:2.55'

    // Onyx SDK
    implementation 'com.onyx.android.sdk:onyxsdk-base:1.7.8'
    implementation 'com.onyx.android.sdk:onyxsdk-device:1.2.32'
    implementation 'com.onyx.android.sdk:onyxsdk-pen:1.4.12'

    // OkHttp, Retrofit and GSON
    ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.15.2'
    implementation 'com.squareup.moshi:moshi:1.15.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.11.0'
    // Deprecated, use suspend functions
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    // Firebase integrations
    implementation platform('com.google.firebase:firebase-bom:33.12.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    // Test dependencies
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    // Temporary (?) fix for https://github.com/gaborauth/toolsboox-android/issues/305
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
}

apply plugin: 'com.google.gms.google-services'
